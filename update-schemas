#!/usr/bin/env perl
use strict;
use warnings;
use Path::Tiny;
use HTTP::Tiny;
use YAML::XS 0.87 'LoadFile';
use JSON::Schema::Modern;

local $YAML::XS::Boolean = 'JSON::PP';

my %files = (
  # metaschema for json schemas contained within openapi documents
  # TODO: switch to /latest, when it is available
  'oas/dialect/base.schema.json'  => 'https://spec.openapis.org/oas/3.1/dialect/base',

  # vocabulary definition
  # TODO: switch to /latest, when it is available
  'oas/meta/base.schema.json'  => 'https://spec.openapis.org/oas/3.1/meta/base',

  # openapi document schema + custom json schema dialect
  #'oas/schema-base.json' => 'https://spec.openapis.org/oas/3.1/schema-base',
  'oas/schema-base.json' => 'https://spec.openapis.org/oas/3.1/schema-base/latest',

  # the main openapi document schema
  #'oas/schema.json' => 'https://spec.openapis.org/oas/3.1/schema',
  'oas/schema.json' => 'https://spec.openapis.org/oas/3.1/schema/latest',

  'oas/LICENSE' => 'https://raw.githubusercontent.com/OAI/OpenAPI-Specification/main/LICENSE',
);

foreach my $target (keys %files) {
  my $source = $files{$target};
  $target = path('share', $target);
  $target->parent->mkpath;

  my $response = HTTP::Tiny->new->get($source);
  die "Failed to fetch $source: $response->{status} $response->{reason}" if not $response->{success};

  $target->spew($response->{content});
}

my $js = JSON::Schema::Modern->new(validate_formats => 1);
$js->add_schema($files{$_} => LoadFile('share/'.$_)) foreach grep /json$/, keys %files;

foreach my $uri (values %files) {
  next if $uri =~ /LICENSE$/;
  print "# validating $uri\n" if $ENV{DEBUG};

  my $document = $js->_fetch_from_uri($uri)->{document};
  my $result = $js->evaluate($document->schema, $document->metaschema_uri);

  die $js->_json_decoder->pretty->encode($result) if not $result;
}
